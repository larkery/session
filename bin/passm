#!/run/current-system/sw/bin/perl

use strict;
use warnings;
use v5.24;

use Getopt::Long;
use Pod::Usage;

my @fields = ();
my $do_copy;
my $do_print;
my $credentials;
my $do_open;
my $help;

GetOptions(
    "copy" => \$do_copy,
    "print" => \$do_print,
    "credentials-file=s" => \$credentials,
    "open" => \$do_open,
    "field=s" => \@fields,
    "help|?" => \$help
    ) or pod2usage(2);

pod2usage(1) if $help;

my $entry = $ARGV[0];

exit unless $entry;

push @fields, "password" unless scalar @fields;

my ($password, @data) = `pass show "$entry"` or die('error from invoking pass');

chomp $password;

my $notes = "";
my %fieldvals = ();

$fieldvals{'password'} = $password;

for my $datum (@data) {
    chomp $datum;
    if ($datum =~ /([^:]+):(.+)/) {
        $fieldvals{$1} = $2;
    } else {
        $notes = $notes . $datum . "\n";
    }
}

for my $field (@fields) {
    my $value = $fieldvals{$field};
    if ($value) {
        say $value if $do_print;
        # copy to clipboard
        if ($do_copy) {
            system 'notify-send', $entry, "copied $field";
            open(my $xclip, "| xclip -quiet -l 1 -selection primary");
            print $xclip $value;
            close($xclip);
        }
    }
}

if ($credentials) {
    open(my $fh, '>', $credentials);
    my $un = $fieldvals{'smb-username'} || $fieldvals{'username'} || $fieldvals{'user'};
    print $fh "username=" . $un . "\n" if $un;
    print $fh "domain=" . $fieldvals{'smb-domain'} . "\n" if $fieldvals{'smb-domain'};
    print $fh "password=" . $fieldvals{'password'} . "\n" if $fieldvals{'password'};
    close($fh);
    # we need to kill the file later
    my $pid;
    open STDIN, '/dev/null';
    open STDOUT, '>>/dev/null';
    open STDERR, '>>/dev/null';
    unless ($pid = fork) {
        unless (fork) {
            sleep 5;
            unlink $credentials;
            exit 0;
        }
        exit 0;
    }
    waitpid($pid, 0);
}

if ($do_open) {
    sub xtype {
        my $text = shift;
        open(my $fh, "|xdotool -");
        print $fh "type $text";
        close $fh;
    }

    my $cmd = "yad --width 200 --title='pass: $entry' --text '$entry: $notes' --form ";
    for my $field (keys %fieldvals) {
        $cmd = $cmd .
            "--field='$field:fbtn' \"echo '$field'\" ";
    }

    open(my $yad, "$cmd --button=gtk-cancel:0 --mouse --no-focus |");
    while (<$yad>) {
        chomp;
        if ($_ eq "url") {
            my $url = $fieldvals{$_};

            unless ($url =~ /:/) {
                $url = "http://$url";
            }

            system "xdg-open", ($url)
        } else {
            xtype $fieldvals{$_} if $fieldvals{$_};
        }
    }
}

__END__
=head1 NAME

passm - a pass wrapper

=head1 SYNOPSIS

passm [--copy] [--print] [--open] [--field x]* [--credentials-file <path>] entry-name
