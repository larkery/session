#!/usr/bin/env zsh

NEW=$(notmuch count -- "tag:new")
NEW_IN=$(notmuch count -- "tag:new and not tag:sent")

if [[ $NEW -gt 0 ]]; then
    if [[ $NEW_IN -gt 0 ]]; then
        notmuch search --format=json --output=summary tag:new and not tag:sent |
        jq -r '.[] | [.authors, .subject] | @sh' |
        while read -r line; do
            words=(${(z)line})
            notify-send -i mail-read "${(Q)words[1]}" "${(Q)words[2]}"
        done
    fi
    CHANGED=("${(@f)$($MAIL_DIR/.notmuch/hooks/refile)}")

    notmuch tag -new -- tag:new

    # if the refiler changed some stuff, we should probably sync again
    if [[ $#CHANGED -gt 1 ]]; then
        echo "$#CHANGED" "$CHANGED"
        pmbsync $CHANGED 2>&1
        ## DANGER WILL ROBINSON
        notmuch new
    fi
fi
